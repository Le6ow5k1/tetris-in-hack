class Game {
  field int frameMs;
  field int tact;
  field Block fallingBlock;
  field int blockDirection;

  constructor Game new(int frameMsIn) {
    let frameMs = frameMsIn;
    let tact = 1;
    let fallingBlock = Block.new(Shape.new(1, 0), Field.blockRespawnX(), Field.minY());
    let blockDirection = 0;
    do Field.draw();

    return this;
  }

  method void run() {
    var char key;
    var boolean exit;
    let exit = false;

    do fallingBlock.draw(true);

    while (~exit) {
      let blockDirection = 0;

      let key = Keyboard.keyPressed();
      if (key = 81)  { let exit = true; }          // q key
      if (key = 131) { let blockDirection = 1; }   // up arrow
      if (key = 133) { let blockDirection = 2; }   // down arrow
      if (key = 130) { let blockDirection = 3; }   // left arrow
      if (key = 132) { let blockDirection = 4; }   // right arrow

      if (tact = 8) {
      do Game.makeBlockMove(fallingBlock, blockDirection);
        do Game.makeBlockFall(fallingBlock);
        let tact = 1;

        do newFallingBlockIfNeeded();
      }
      if (~(tact = 8)) {
        do Game.makeBlockMove(fallingBlock, blockDirection);

        do newFallingBlockIfNeeded();
      }

      let tact = tact + 1;
      do Sys.wait(frameMs);
    }

    return;
  }

  method void newFallingBlockIfNeeded() {
    if (fallingBlock.reachedBottom()) {
      let fallingBlock = Game.newBlock();
      do fallingBlock.draw(true);
    }

    return;
  }

  function Block newBlock() {
    return Block.new(Shape.new(1, 0), Field.blockRespawnX(), Field.minY());
  }

  function void moveBlock(Block block, int direction) {
    if (direction = 1) { do block.rotate(); }
    if (direction = 2) { do block.moveDown(); }
    if (direction = 3) { do block.moveLeft(); }
    if (direction = 4) { do block.moveRight(); }

    return;
  }

  function void makeBlockFall(Block block) {
    do block.draw(false);
    do block.moveDown();
    do block.draw(true);

    return;
  }

  function void makeBlockMove(Block block, int direction) {
    do block.draw(false);
    do Game.moveBlock(block, direction);
    do block.draw(true);

    return;
  }
}
